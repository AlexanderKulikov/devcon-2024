
@Обработчик
метод ПроверитьНаличиеКлючейДоступа(Ключи: ЧитаемыйМассив<КлючДоступаПоОрганизации.Объект>, ПользователиДляПроверки: ЧитаемыйМассив<Пользователи.Объект>): 
    ЧитаемоеСоответствие<КлючДоступаПоОрганизации.Объект, ЧитаемаяКоллекция<Пользователи.Объект|Пользователи.Ссылка>>
    
    знч Организации = <Организации.Ссылка>[]
    для Ключ из Ключи
        Организации.Добавить(Ключ.Организация)
    ;
    
    знч Пользователи = <Пользователи.Ссылка>[]
    для Пользователь из ПользователиДляПроверки
        Пользователи.Добавить(Пользователь.Ссылка)
    ;
    
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Представители.Организация КАК Организация,
            Представители.Пользователь КАК Пользователь
        ИЗ
            Представители КАК Представители
        ГДЕ
            Представители.Организация В (%Организации)
            И Представители.Пользователь В (%Пользователи)
    }
    
    знч ПредставителиОрганизаций = <Организации.Ссылка, Массив<Пользователи.Ссылка>>{:}
    
    знч РезультатЗапроса = Запрос.Выполнить()
    
    для СтрокаРезультата из РезультатЗапроса
        ПредставителиОрганизаций.ВставитьЕслиОтсутствует(СтрокаРезультата.Организация, <Пользователи.Ссылка>[])
        ПредставителиОрганизаций.Получить(СтрокаРезультата.Организация).Добавить(СтрокаРезультата.Пользователь)
    ;
    
    РезультатЗапроса.Закрыть()
    
    знч Результат = <КлючДоступаПоОрганизации.Объект, Массив<Пользователи.Ссылка>>{:}
    
    для Ключ из Ключи
        знч МассивПользователей = ПредставителиОрганизаций.ПолучитьИлиУмолчание(Ключ.Организация)
        если МассивПользователей != Неопределено
            Результат.Вставить(Ключ, МассивПользователей)
        ;
    ;       
    
    возврат Результат
;